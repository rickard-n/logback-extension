ext {
	logbackVersion = '0.9.30'
	mockitoVersion = '1.9.5'
	junitVersion = '4.11'
    tomcatVersion = '6.0.20'

	libraries = [
    		logback: [
             	[group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion],
                [group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion],
                [group: 'ch.qos.logback', name: 'logback-access', version: logbackVersion],
    		],
    		test: [
				[group: 'junit', name: 'junit', version: junitVersion],
				[group: 'org.mockito', name: 'mockito-core', version: mockitoVersion],
				[group: 'org.mockito', name: 'mockito-all', version: mockitoVersion],
            ]
    ]
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
    apply plugin: 'maven'


    sourceCompatibility= 1.6
    group = 'se.svt.logback'
    version = '1.0'

	configurations {
      provided
      provided.extendsFrom(compile)
    }

	idea {
		module {
			scopes.PROVIDED.plus += configurations.provided
        	downloadSources = true
		}
	}

	repositories {
			mavenLocal()
			mavenCentral()
	}

	dependencies {
		compile libraries.logback
        provided group: 'org.apache.tomcat', name: 'catalina', version: tomcatVersion
		testCompile libraries.test
		testCompile group: 'org.apache.tomcat', name: 'coyote', version: tomcatVersion
		testCompile group: 'org.springframework', name: 'spring-test', version: '3.2.1.RELEASE'
		testCompile group:'com.google.guava', name: 'guava', version: '14.0'
	}

	test {
		testLogging {
			events "failed", "standard_out"
		}
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }

        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }

    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar
    }
}



task wrapper(type: Wrapper) {
	gradleVersion = '1.4'
}
